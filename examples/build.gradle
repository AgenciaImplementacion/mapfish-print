apply plugin: 'war'
apply plugin: 'org.akhikhl.gretty'


repositories {
    maven {url "http://repo.boundlessgeo.com/main"}
    maven {url "http://maven.restlet.org"}
}

dependencies {
    testCompile (project(':core'))
}

configurations {
}

def examplesDir = "${project(':examples').projectDir}/src/test/resources/examples"

gretty {
    // configuration to serve a web-app containing test data required for
    // the integration tests
    servletContainer = "jetty7"
    integrationTestTask = "integrationTest"
}

farm {
    // configuration to start a server running mfp, geoserver and the web-app
    // containing test data. start with `./gradlew examples:farmRun`
    systemProperty "path_to_examples", examplesDir
    jvmArgs = ["-Xmx512M", "-XX:MaxPermSize=512m"]

    // FIXME do not output geoserver logs
    webapp "org.geoserver.web:gs-web-app:2.8.2"
    webapp ":core"
    webapp ":examples"
}

// prevent that the tests target can be directly called. the integration tests
// must be run with `./gradlew examples:farmIntegrationTest` to make sure the
// server is running
test { }
test.onlyIf{false}

task integrationTest(type: Test) {
    testLogging {
        exceptionFormat = 'full'
        showCauses true
        showExceptions true
        showStackTraces true
    }
}
integrationTest.dependsOn ':core:compileJava'

afterEvaluate {
    build.dependsOn project.tasks['farmIntegrationTest']
}
